plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java-library'
    id 'jacoco'
    id 'idea'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = '17'
}


repositories {
    mavenCentral()
}

tasks.named('test') {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework:spring-webflux'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'


    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

bootJar.enabled = false
jar.enabled = true

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                name = project.name
                description = 'Lightweight HATEOAS library designed to work with Spring Webflux.'
                url = 'https://github.com/kamillionlabs/hateoflux'


                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'younes'
                        name = 'Younes El Ouarti'
                        email = 'younes@kamillionlabs.de'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/kamillionlabs/hateoflux.git'
                    developerConnection = 'scm:git:ssh://git@github.com/kamillionlabs/hateoflux.git'
                    url = 'https://github.com/kamillionlabs/hateoflux'
                }

            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")

            credentials {
                username = System.getenv('OSSRH_USERNAME') ?: ''
                password = System.getenv('OSSRH_PASSWORD') ?: ''
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

ext['signing.gnupg.keyName'] = System.getenv('SIGNING_KEY_ID') ?: ''
ext['signing.gnupg.passphrase'] = System.getenv('SIGNING_PASSWORD') ?: ''
ext['signing.gnupg.options'] = ['--batch', '--yes', '--pinentry-mode', 'loopback']
